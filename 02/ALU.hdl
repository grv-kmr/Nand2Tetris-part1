// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    //for zx
    Mux16(a = x, b = false, sel = zx, out = zxout);

    //for nx
    Not16(in = zxout, out = notzxout);
    Mux16(a=zxout, b=notzxout, sel= nx, out= nxout);
    
    //for zy
    Mux16(a = y, b = false, sel = zy, out = zyout);

    //for ny
    Not16(in = zyout, out = notzyout);
    Mux16(a=zyout, b=notzyout, sel= ny, out= nyout);
    
    //for f
    And16(a = nxout, b = nyout, out =f1out);
    Add16(a = nxout, b = nyout, out =f2out);
    Mux16(a = f1out, b = f2out, sel = f, out = fout);

    //for no
    Not16(in = fout, out = notfout);
    Mux16(a = fout, b = notfout, sel = no,out[0..7] = zr1, out[8..15] = zr2, out[15] = ng, out = out);

    //for zr
    Or8Way(in= zr1, out= zr10);
    Or8Way(in=zr2 , out= zr11);
    Or(a = zr10,b = zr11, out = zr13);
    Not(in = zr13, out = zr);

    //// Replace this comment with your code.
}